"Gazelle extension for Kotlin"

module(name = "gazelle_kt")

bazel_dep(name = "gazelle", version = "0.44.0")

# The following deps are using the same versions as upstream gazelle
bazel_dep(name = "rules_go", version = "0.50.1")
bazel_dep(name = "rules_proto", version = "4.0.0")
bazel_dep(name = "bazel_skylib", version = "1.5.0")
bazel_dep(name = "package_metadata", version = "0.0.3")

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.24.0")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")

go_deps.module_override(
    patches = ["//patches:go-tree-sitter-cc_library.patch"],
    path = "github.com/smacker/go-tree-sitter",
)

go_deps.module_override(
    patch_strip = 1,
    patches = [
        "//patches:aspect-cli.only-kotlin.patch",
        "//patches:aspect-cli.cgo-binding.patch",
    ],
    path = "github.com/aspect-build/aspect-cli",
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "tree-sitter-kotlin",
    build_file_content = """\
package(default_visibility = ["//visibility:public"])
filegroup(name = "srcs", srcs = glob(["src/**/*.c", "src/**/*.h"]))
""",
    sha256 = "7dd60975786bf9cb4be6a5176f5ccb5fed505f9929a012da30762505b1015669",
    strip_prefix = "tree-sitter-kotlin-0.3.8",
    urls = ["https://github.com/fwcd/tree-sitter-kotlin/archive/0.3.8.tar.gz"],
)

# See https://bazel.build/external/extension#overriding_and_injecting_module_extension_repos
inject_repo(go_deps, "tree-sitter-kotlin")

# Throw away BUILD files found in aspect-cli.
# They depend on go modules like tree-sitter-typescript.
go_deps.gazelle_override(
    build_file_generation = "clean",
    path = "github.com/aspect-build/aspect-cli",
)
use_repo(
    go_deps,
    "com_github_aspect_build_aspect_cli",
    "com_github_bazel_contrib_rules_jvm",
    "com_github_emirpasic_gods",
    "com_github_rs_zerolog",
    "com_github_smacker_go_tree_sitter",
    "net_starlark_go",
)

### Dev Dependencies below ###
bazel_dep(name = "bazel_env.bzl", version = "0.4.0", dev_dependency = True)
bazel_dep(name = "rules_multitool", version = "1.6.0", dev_dependency = True)

multitool = use_extension("@rules_multitool//multitool:extension.bzl", "multitool", dev_dependency = True)
multitool.hub(lockfile = "//tools:tools.lock.json")
use_repo(multitool, "multitool")
